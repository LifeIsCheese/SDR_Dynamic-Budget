title: Reichsbank
new-page: true
is-card: true
tags: govt_affairs
frequency: 500
view-if: spd_in_government = 1 and finance_minister_party = "SPD" and reichsbank_timer == 0
on-arrival: reichsbank_timer += 4; month_actions += 1
card-image: img/Reichsbank.jpg

= Reichsbank

[? if hjalmar_plot != "successful": Hjalmar Schacht opinion: Hateful. ?]

[? if hjalmar_plot == "successful": Now that the Reichsbank is controlled by Hans Luther, perhaps now the Reichsbank will heed our policies.?]

[? if hjalmar_plot == "successful": Hans Luther opinion: [+ hans_string +]. [? if hans_opinion <= 2 : We can persuade him by talking to him directly. ?] ?]

Reichsbank reserve requirement: 40%

- @raise_interest_rates
- @lower_interest_rates
- @bribe: Maybe 1 million Reichsmarks can change his mind.
- @troll: Maybe 1 million Reichsmarks can change his mind.
- @reform_bond: Reform the Young Plan bonds.
- @root: No, we're going to our own thing.
- @easy_discard

@raise_interest_rates
title: Increase interest rates
subtitle: Affects middle class and rural much more than workers. [? if interest_rates >= 2 : Interest rates are too high. ?] [? if reichsbank_a != 1 : Hjalmar Schacht would never listen to us. ?]
choose-if: 2 > interest_rates and reichsbank_a == 1 and hans_opinion > 2
on-arrival: inflation -= 1.5 if inflation <= 0; inflation -= 1 if inflation > 0; inflation -= 2 if inflation >= 4; inflation -= 2 if inflation >= 8; unemployed += inflation; workers_spd -= 1; unemployed_spd -= 1; rural_spd -= 1*(1-dissent); old_middle_spd -= 3*(1-dissent); new_middle_spd -= 4; capital_strike_progress += 1; interest_rates +=1; economic_growth -= 0.5 if economic_growth <= 0; economic_growth -= 1 if economic_growth > 0; economic_growth -= 1 if economic_growth >= 3; economic_growth -= 2 if economic_growth >= 8; month_actions +=1; hans_opinion -=1 if economic_growth < 3

Increasing interest rates decreases inflation, especially if inflation is high. [? if economic_growth < 3: Hans luther reluctanctly agrees, but thinks less of us because he thinks economic growth is too low?]

@lower_interest_rates
title: Decrease interest rates
subtitle: Affects middle class and rural much more than workers [? if interest_rates <= -2 : Interest rates are too low. ?] [? if reichsbank_a != 1 : Hjalmar Schacht would never listen to us. ?]
choose-if: interest_rates > -2 and reichsbank_a == 1 and hans_opinion > 2
on-arrival: inflation += 1.5 if inflation <= 0; inflation += 1 if inflation > 0; inflation += 1 if inflation >= 4; inflation += 1 if inflation >= 8; workers_spd += 1; unemployed_spd += 1; rural_spd += 1*(1-dissent); old_middle_spd += 3*(1-dissent); new_middle_spd += 4; capital_strike_progress -= 1; interest_rates -=1; economic_growth += 0.5 if economic_growth <= 0; economic_growth += 1 if economic_growth > 0; economic_growth += 1 if economic_growth >= 3; economic_growth += 2 if economic_growth >= 8; month_actions +=1; hans_opinion -= 1 if inflation >= 5

Decreasing interest rates stimulates economic growth, but increases inflation. [? if inflation >= 5: Hans luther reluctanctly agrees, but thinks less of us because he thinks inflation is too high?]

@bribe
view-if: reichsbank_a == 1 and hans_opinion <= 2
title: With hans being uncooperative, perhaps he needs an "incentive".
subtitle: -1 resources
choose-if: resources > 0
on-arrival: hans_opinion +=1; resources -=1; barmat +=1 if barmat <= 2

By transferring our funds in Hans Luther's wallet, he may think more highly of us.

@troll
view-if: reichsbank_a == 0
title: Perhaps he needs an, "incentive"
subtitle: -1 resources
choose-if: resources > 0
on-arrival: resources -=1; barmat +=1 if barmat <= 2

@reform_bond
view-if: reparations_paused == 1 and bond_reform == 0
subtitle: [? if in_weimar_coalition or in_grand_coalition and z_relation <= 30 : The center is reluctant on this ?] [? if in_grand_coalition and dvp_relation <= 40 : The DVP opposes this. ?]
choose-if: in_spd_majority or (in_weimar_coalition and z_relation > 30) or (in_grand_coalition and z_relation > 30 and dvp_relation > 40)
on-arrival: bond_reform = 1

Originally set up as a way to reduce burden on our reparations, we can reform this to work more like actual bonds. A sort of, Reichsbond.
